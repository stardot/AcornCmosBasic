
;                                   FILE D     function call > CBAS0A

FNMISS PLA
 STA LINE+1
 PLA
 STA LINE
 BRK
 = &1D
 = "No such ",TFN,"/",TPROC
 BRK
FNDEF LDA TXTP ;WORK+2 has been set by LOOKFN
 STA LINE+1
 CLR LINE
FNFIND LDYIM 1
 LDAIY LINE
 BMI FNMISS
 LDYIM 3
FNFINS INY
 LDAIY LINE
 CMPIM " "
 BEQ FNFINS
 CMPIM TDEF
 BEQ DEFFND
NEXLIN LDYIM 3
 LDAIY LINE
 CLC
 ADC LINE
 STA LINE
 BCC FNFIND
 INC LINE+1
 BRA FNFIND
DEFFND INY
 STY CURSOR
 JSR SPACES
 TYA
 TAX
 CLC
 ADC LINE
 LDY LINE+1
 BCC FNFDIN
 INY
 CLC
FNFDIN SBCIM 0
 STA WORK+5
 TYA
 SBCIM 0
 STA WORK+6
 LDYIM 1
FNFDLK INX
 LDAIY WORK+5
 CMPIY WORK
 BNE NEXLIN
 INY
 CPY WORK+2
 BNE FNFDLK
 LDAIY WORK+5
 JSR WORDCQ
 BCS NEXLIN
 TXA
 TAY
 JSR CLYADP
 JSR CREAFN
 LDXIM 1
 JSR CREAX
 LDA LINE
 STAI FSA
 LDYIM 1
 LDA LINE+1
 STAIY FSA
 INY
 JSR FSAPY
 JMP FNGO
FNCALL BRK
 = &1E
 = "Bad call"
 BRK
FN LDAIM TFN
FNBODY STA TYPE
 TSX
 TXA
 CLC
 ADC AESTKP
 JSR HIDEC
 TXA
 STAI AESTKP
 LDYIM 0
FNPHLP INX
 INY
 LDAAX &0100
 STAIY AESTKP
 CPXIM &FF
 BNE FNPHLP
 TXS
 LDA TYPE
 PHA
 LDA CURSOR
 PHA
 LDA LINE
 PHA
 LDA LINE+1
 PHA
 LDA AECUR
 TAX
 CLC
 ADC AELINE
 LDY AELINE+1
 BCC FNNOIC
 INY
 CLC
FNNOIC SBCIM 1
 STA WORK
 TYA
 SBCIM 0
 STA WORK+1
 LDYIM 2
 JSR WORDLP
 CPYIM 2
 BEQ FNCALL
 STX AECUR
 JSR LOOKFN
 BNE FNGOA
 JMP FNDEF
FNGOA LDAI IACCL
 STA LINE
 LDYIM 1
 LDAIY IACCL
 STA LINE+1
FNGO LDAIM 0
 PHA
 CLR CURSOR
 JSR SPACES
 CMPIM "("
 BEQ FNARGS
 DEC CURSOR
DOFN LDA AECUR
 PHA
 LDA AELINE
 PHA
 LDA AELINE+1
 PHA
 JSR STMT ;type now contains type
 PLA
 STA AELINE+1
 PLA
 STA AELINE
 PLA
 STA AECUR
 PLA
 BEQ DNARGS
 STA WORK+8
GTARGS JSR POPWRK
 JSR STORST
 DEC WORK+8
 BNE GTARGS
DNARGS PLA
 STA LINE+1
 PLA
 STA LINE
 PLA
 STA CURSOR
 PLA
 LDAI AESTKP
 TAX
 TXS
 LDYIM 0
FNPLLP INY
 INX
 LDAIY AESTKP
 STAAX &0100
 CPXIM &FF
 BNE FNPLLP
 TYA
 ADC AESTKP
 STA AESTKP
 BCC FNPL
 INC AESTKP+1
FNPL LDA TYPE
 RTS
FNARGS LDA AECUR ;hpush aeline
 PHA
 LDA AELINE
 PHA
 LDA AELINE+1
 PHA
 JSR CRAELV
 BEQ ARGMAT
 LDA AECUR
 STA CURSOR
 PLA ;hpull aeline
 STA AELINE+1
 PLA
 STA AELINE
 PLA
 STA AECUR
 PLX ;hpull args
 LDA IACCN ;hpush lvalue
 PHA
 LDA IACCM
 PHA
 LDA IACCL
 PHA
 INX ;args:=args+1
 PHX ;hpush args
 JSR RETINF ;bpush return info
 JSR CHKCOM
 BEQ FNARGS
 CMPIM ")"
 BNE ARGMAT
 LDAIM 0 ;hpush 0
 PHA
 JSR AESPAC
 CMPIM "("
 BNE ARGMAT
FNARGP JSR EXPR
 JSR PHTYPE ;bpush val
 LDA TYPE ;bpush type
 STA IACCH
 JSR PHACC
 PLX ;hpull args;inc args;hpush args
 INX
 PHX
 JSR COMCHK
 BEQ FNARGP
 CMPIM ")"
 BNE ARGMAT
 PLA
 PLA
 STA COEFP
 STA COEFP+1
 CPX COEFP
 BEQ FNARGZ
ARGMAT LDXIM &FB
 TXS
 PLA
 STA LINE+1
 PLA
 STA LINE
 BRK
 = &1F
 = "Arguments"
 BRK
FNARGZ JSR POPACC ;bpull type
 PLA ;hpull lv
 STA IACCL
 PLA
 STA IACCM
 PLA
 STA IACCN
 BMI FNARGY
 LDA IACCH
 BEQ ARGMAT
 STA TYPE
 LDXIM WORK
 JSR ACCTOM
 LDA TYPE
 BPL FNARPO
 JSR POPSET
 JSR FLDA
 BRA FNAROP
FNARPO JSR POPACC
FNAROP JSR STORF
 BRA FNARGW
FNARGY LDA IACCH
 BNE ARGMAT
 JSR POPSTR
 JSR STSTRE
FNARGW DEC COEFP
 BNE FNARGZ
 LDA COEFP+1
 PHA
 JMP DOFN
RETINF LDY IACCN
 CPYIM 5
 BCS FNINFO
 LDXIM WORK
 JSR ACCTOM
FNINFO JSR VARIND
 PHP
 JSR PHTYPE
 PLP
 BEQ FNSTRD
 BMI FNSTRD
 LDXIM WORK
 JSR MTOACC
FNSTRD JMP PHACC
VARIND LDY IACCN
 BMI FACSTR
 BEQ VARONE
 CPYIM 5
 BEQ VARFP
 LDYIM 3
 LDAIY IACCL
 STA IACCH
 DEY
 LDAIY IACCL
 STA IACCN
 DEY
 LDAIY IACCL
 TAX
 LDAI IACCL
 STA IACCL
 STX IACCM
 LDAIM &40
 RTS
VARONE LDAIY IACCL
 JMP AYACC
VARFP CLR FACCMG
 CLR FACCXH
 DEY
 LDAIY IACCL
 STA FACCMD
 DEY
 LDAIY IACCL
 STA FACCMC
 DEY
 LDAIY IACCL
 STA FACCMB
 DEY
 LDAIY IACCL
 STA FACCS
 TAY
 LDAI IACCL
 STA FACCX
 BNE VARFPZ
 TYA
 ORA FACCMB
 ORA FACCMC
 ORA FACCMD
 BEQ VARFPX
VARFPZ TYA
 ORAIM &80
VARFPX STA FACCMA
 LDAIM &FF
 RTS
FACSTR CPYIM &80
 BEQ FACSTT
 LDYIM 3
 LDAIY IACCL
 STA CLEN
 BEQ STRRTS
 LDYIM 1
 LDAIY IACCL
 STA WORK+1
 LDAI IACCL
 STA WORK
 LDY CLEN
MOVTOS DEY
 LDAIY WORK
 STAAY STRACC
 TYA
 BNE MOVTOS
STRRTS RTS
FACSTT LDA IACCM ;these two instructions are obsolete!
 BEQ CHRF
 LDYIM 0
FACSTU LDAIY IACCL
 STAAY STRACC
 EORIM &0D
 BEQ FACSTV
 INY
 BNE FACSTU
 TYA
FACSTV STY CLEN
 RTS
CHRD JSR INTFAC
CHRF LDA IACCL
 JMP SINSTR
 LNK CBAS0B
