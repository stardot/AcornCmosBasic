;                                   FILE C     factor -> CBAS09
INKEA JSR INTFAC
 LDAIM &81
 LDX IACCL
 LDY IACCM
 JMP OSBYTE
FRNDAA JSR FRNDAB
FRND CLR FACCS
 CLR FACCXH
 CLR FACCMG
 LDAIM &80
 STA FACCX
 LDYIM 0
 LDXIM 3
MTOFAC EORAY SEED
 STAAX FACCMA
 INY
 DEX
 BPL MTOFAC
 JMP NRMTDY
RNDB INC AECUR
 JSR INTBRA
 LDA IACCH
 BMI RNDSET
 ORA IACCN
 ORA IACCM
 BNE RNDBB
 LDA IACCL
 BEQ FRND
 CMPIM 1
 BEQ FRNDAA
RNDBB JSR IFLT
 JSR PHFACC
 JSR FRNDAA
 JSR POPSET
 JSR IFMUL
 JSR IFIX
 JSR INCACC
 BRA RPTRX
RNDSET LDXIM SEED
 JSR ACCTOM
 LDAIM &40
 STA SEED+4
 RTS
RND LDY AECUR
 LDAIY AELINE
 CMPIM "("
 BEQ RNDB
 JSR FRNDAB
 LDXIM SEED
MTOACC LDAAX 0
 STA IACCL
 LDAAX 1
 STA IACCM
 LDAAX 2
 STA IACCN
 LDAAX 3
 STA IACCH
RPTRX LDAIM &40
 RTS
NOT JSR INTFAC
 LDXIM 3
NOTLOP LDAAX IACCL
 EORIM &FF
 STAAX IACCL
 DEX
 BPL NOTLOP
 BRA RPTRX
POS JSR VPOS
 STX IACCL
 RTS
USR JSR INTFAC
 JSR USER
 STA IACCL
 STX IACCM
 STY IACCN
 PHP
 PLA
 STA IACCH
 CLD
 BRA RPTRX
VPOS LDAIM &86
 JSR OSBYTE
 TYA
VPOSX JMP SINSTK
EXT LDAIM 2
 BRA RPTR+2
RPTR LDAIM 0
 PHA
 JSR CHANN
 LDXIM IACCL
 PLA
 JSR OSARGS
 BRA RPTRX
BGET JSR CHANN
 JSR OSBGET
 BRA VPOSX
OPENU LDAIM &40
 BRA F
OPENO LDAIM &80
 BRA F
OPENI LDAIM &C0
F PHA
 JSR FACTOR
 BNE OPENE
 JSR OSSTRT
 LDXIM STRACC
 LDYIM /STRACC
 PLA
 JSR OSFIND
 BRA VPOSX
OPENE JMP LETM
PI JSR ASINAA
 INC FACCX;resets -ve status, A already negative and unharmed
 RTS
EVAL JSR FACTOR
 BNE EVALE
 INC CLEN
 LDY CLEN
 LDAIM &0D
 STAAY STRACC-1
 JSR PHSTR
 LDA AELINE
 PHA
 LDA AELINE+1
 PHA
 LDA AECUR
 PHA
 LDY AESTKP
 LDX AESTKP+1
 INY
 STY AELINE
 STY WORK
 BNE EVALX
 INX
EVALX STX AELINE+1
 STX WORK+1
 JSR MATCHY
 CLR AECUR
 JSR EXPR
 JSR POPSTX
VALOUT PLA
 STA AECUR
 PLA
 STA AELINE+1
 PLA
 STA AELINE
 LDA TYPE
 RTS
EVALE JMP LETM
VAL JSR FACTOR
 BNE EVALE
VALSTR LDX CLEN
 CLRAX STRACC
 LDA AELINE
 PHA
 LDA AELINE+1
 PHA
 LDA AECUR
 PHA
 CLR AECUR
 CLR AELINE ;(of course stracc=0)
 LDAIM /STRACC
 STA AELINE+1
 JSR AESPAC
 CMPIM "-"
 BEQ VALMIN
 CMPIM "+"
 BNE VALNUB
 JSR AESPAC
VALNUB DEC AECUR
 JSR FRDD
 BRA VALTOG
VALMIN JSR AESPAC
 DEC AECUR
 JSR FRDD
 BCC VALTOG
 JSR VALCMP
VALTOG STA TYPE
 BRA VALOUT
INT JSR FACTOR
 BEQ FACTE
 BPL INTX
 LDA FACCS
 PHP
 JSR FFIX
 PLP
 BPL INTF
 LDA FWRKMA
 ORA FWRKMB
 ORA FWRKMC
 ORA FWRKMD
 BEQ INTF
 JSR FINEG
 JSR FINC
 JSR FINEG
INTF JSR IFIX+3
 LDAIM &40
INTX RTS
ASC JSR FACTOR
 BNE FACTE
 LDA CLEN
 BEQ TRUE
 LDA STRACC
SINSTJ JMP SINSTK
INKEY JSR INKEA
 TYA
 BNE TRUE
 TXA
 JMP AYACC
FACTE JMP LETM
EOF JSR CHANN
 TAX
 LDAIM &7F
 JSR OSBYTE
 TXA
 BEQ TRUTWO
TRUE LDXIM &FF
TRUTWO STX IACCL
 STX IACCM
 STX IACCN
 STX IACCH
SGNINT LDAIM &40
 RTS
FALSE LDXIM 0
 BRA TRUTWO
SGNFLT JSR FTST
 BEQ FALSE
 BPL SGNPOS
 BRA TRUE
SGN JSR FACTOR
 BEQ FACTE
 BMI SGNFLT
 LDA IACCH
 ORA IACCN
 ORA IACCM
 ORA IACCL
 BEQ SGNINT
 LDA IACCH
 BMI TRUE
SGNPOS LDAIM 1
SGNSIN BRA SINSTJ
POINT JSR INEXPR
 JSR PHACC
 JSR COMEAT
 JSR INTBRA
 LDA IACCL
 PHA
 LDX IACCM
 JSR POPACC
 STX IACCH
 PLA
 STA IACCN
 LDYIM 0
 LDXIM IACCL
 LDAIM &09
 JSR OSWORD
 LDA IACCL+4
 BMI TRUE
 BRA SGNSIN
INSTR JSR EXPR
 BNE FACTE
 CPXIM ","
 BNE INSTRE
 INC AECUR
 JSR PHSTR
 JSR EXPR
 BNE FACTE
 LDAIM 1
 STA IACCL
 INC AECUR
 CPXIM ")"
 BEQ INSTRG
 CPXIM ","
 BEQ INSTRH
INSTRE JMP COMERR
INSTRH JSR STINBR
 JSR POPSTR
INSTRG LDX IACCL
 BNE INSTRF
 LDXIM 1
INSTRF STX IACCL
 TXA
 DEX
 STX IACCH
 CLC
 ADC AESTKP
 STA WORK
 LDAIM 0
 ADC AESTKP+1
 STA WORK+1
 LDAI AESTKP
 SEC
 SBC IACCH
 BCC INSTRY
 SBC CLEN
 BCC INSTRY
 ADCIM 0
 STA IACCM
 JSR POPSTX
INSTRL LDYIM 0
 LDX CLEN
 BEQ INSTRO
INSTRM LDAIY WORK
 CMPAY STRACC
 BNE INSTRN
 INY
 DEX
 BNE INSTRM
INSTRO LDA IACCL
INSTRP JMP SINSTK
INSTRY JSR POPSTX
INSTRZ LDAIM 0
 BRA INSTRP
INSTRN INC IACCL
 DEC IACCM
 BEQ INSTRZ
 INC WORK
 BNE INSTRL
 INC WORK+1
 BRA INSTRL
ABSE JMP LETM
ABS JSR FACTOR
 BEQ ABSE
 BMI FABS
ABSCOM BIT IACCH
 BMI COMPNO
 BRA COMPDN
FABS CLR FACCS
 RTS ;status flags unchanged!
FSUB JSR FXSUB
FNEG LDA FACCMA
 BEQ FNEGX
 LDA FACCS
 EORIM &80
 STA FACCS
FNEGX LDAIM &FF
 RTS
UNMINS JSR UNPLUS
VALCMP BEQ ABSE
 BMI FNEG
COMPNO SEC
 LDAIM 0
 TAY
 SBC IACCL
 STA IACCL
 TYA
 SBC IACCM
 STA IACCM
 TYA
 SBC IACCN
 STA IACCN
 TYA
 SBC IACCH
 STA IACCH
COMPDN LDAIM &40
 RTS
DATAST JSR AESPAC
 CMPIM """"
 BEQ QSTR
 LDXIM 0
DATASL LDAIY AELINE
 STAAX STRACC
 INY
 INX
 CMPIM &0D
 BEQ QSTRF
 CMPIM ","
 BNE DATASL
QSTRF DEY
QSTRE DEX
 STX CLEN
 STY AECUR
 LDAIM 0
 RTS
QSTR LDXIM 0
QSTRP INY
QSTRL LDAIY AELINE
 CMPIM &0D
 BEQ QSTREG
 STAAX STRACC
 INY
 INX
 CMPIM """"
 BNE QSTRL
 LDAIY AELINE
 CMPIM """"
 BEQ QSTRP
 BNE QSTRE
QSTREG JMP NSTNG
FACTOR LDY AECUR
 INC AECUR
 LDAIY AELINE
 CMPIM " "
 BEQ FACTOR
 CMPIM "-"
 BEQ UNMINS
 CMPIM """"
 BEQ QSTR
 CMPIM "+"
 BNE DOPLUS
UNPLUS JSR AESPAC
DOPLUS CMPIM TOPENU
 BCC TSTVAR
 CMPIM TEOF+1
 BCS FACERR
 JMP DISPATCH
TSTVAR CMPIM "?"
 BCS TSTVB
 CMPIM "."
 BCS TSTN
 CMPIM "&"
 BEQ HEXIN
 CMPIM "("
 BEQ BRA
TSTVB DEC AECUR
 JSR LVCONT
 BEQ ERRFAC
 JMP VARIND
TSTN JSR FRDD
 BCC FACERR
 RTS
ERRFAC LDA BYTESM
 ANDIM 2
 BNE FACERR
 BCS FACERR
 STX AECUR
GETPC LDA PC
 LDY PC+1
 BRA AYACCB
FACERR BRK
 = &1A
 = "No such variable"
BKTERR BRK
 = &1B
 = &8D,")"
HEXDED BRK
 = &1C
 = "Bad Hex"
 BRK
BRA JSR EXPR
 INC AECUR
 CPXIM ")"
 BNE BKTERR
 TAY
 RTS
HEXIN JSR FALSE
 INY
HEXIP LDAIY AELINE
 CMPIM &30
 BCC HEXEND
 CMPIM &3A
 BCC OKHEX
 SBCIM &37
 CMPIM &0A
 BCC HEXEND
 CMPIM &10
 BCS HEXEND
OKHEX ASLA
 ASLA
 ASLA
 ASLA
 LDXIM 3
INLOOP ASLA
 ROL IACCL
 ROL IACCM
 ROL IACCN
 ROL IACCH
 DEX
 BPL INLOOP
 INY
 BNE HEXIP
HEXEND TXA
 BPL HEXDED
 STY AECUR
 LDAIM &40
 RTS
ADC JSR INTFAC
 LDX IACCL
 LDAIM &80
 JSR OSBYTE
 TXA
AYACCB BRA AYACC
TO INY
 LDAIY AELINE
 CMPIM "P"
 BNE FACERR
 INC AECUR
 LDA TOP
 LDY TOP+1
 BRA AYACC
RPAGE LDY TXTP
 LDAIM 0
 BRA AYACC
LENB JMP LETM
LEN JSR FACTOR
 BNE LENB
 LDA CLEN
SINSTK LDYIM 0
AYACC STA IACCL
 STY IACCM
 CLR IACCN
 CLR IACCH
 LDAIM &40
 RTS
COUNT LDA TALLY
 BRA SINSTK
RLOMEM LDA LOMEM
 LDY LOMEM+1
 BRA AYACC
RHIMEM LDA HIMEM
 LDY HIMEM+1
 BRA AYACC
ERL LDY LOLINO+1
 LDA LOLINO
 BRA AYACC
ERR LDAI REPPTR
 BRA SINSTK
GET JSR OSRDCH
 BRA SINSTK
RTIME INY
 LDAIY AELINE
 CMPIM "$"
 BEQ RTIMED
 LDXIM IACCL
 LDYIM 0
 LDAIM 1
 JSR OSWORD
 LDAIM &40
 RTS
RTIMED INC AECUR
 LDAIM 14 ; read time$
 LDXIM STRACC
 LDYIM /STRACC
 CLR STRACC ;ask for long time
 JSR OSWORD
 LDAIM 24 ;answer has fixed length
 BRA LEFTS
GETD JSR OSRDCH
SINSTR STA STRACC
 LDAIM 1
 BRA LEFTS
LEFTD CLC;LEFT$ and RIGHT$ share initial code
RIGHTD PHP ;carry SET on entry implies RIGHT$
 JSR EXPR
 BNE LEFTE
 CPXIM ","
 BNE MIDC
 INC AECUR
 JSR STINBR
 JSR POPSTR
 PLP
 BCS RIGHTT
 LDA IACCL
 CMP CLEN
 BCS LEFTX
LEFTS STA CLEN
LEFTX LDAIM 0
 RTS
RIGHTT LDA CLEN;carry set
 SBC IACCL
 BCC LEFTX
 BEQ LEFTX+2
 TAX
 LDA IACCL
 STA CLEN
 BEQ LEFTX+2
 LDYIM 0
RGHLOP LDAAX STRACC
 STAAY STRACC
 INX
 INY
 DEC IACCL
 BNE RGHLOP
 BRA LEFTX
INKED JSR INKEA
 TXA
 CPYIM 0
 BEQ SINSTR
RNUL LDAIM 0
 BRA LEFTS
LEFTE JMP LETM
MIDC JMP COMERR
MIDD JSR EXPR
 BNE LEFTE
 CPXIM ","
 BNE MIDC
 JSR PHSTR
 INC AECUR
 JSR INEXPR
 LDA IACCL
 PHA
 LDAIM &FF
 STA IACCL
 INC AECUR
 CPXIM ")"
 BEQ MIDTWO
 CPXIM ","
 BNE MIDC
 JSR INTBRA
MIDTWO JSR POPSTR
 PLA
 TAY
 CLC
 BEQ MIDLB
 SBC CLEN
 BCS RNUL
 DEY
 TYA
MIDLB STA IACCN
 TAX
 LDYIM 0
 LDA CLEN
 SEC
 SBC IACCN
 CMP IACCL
 BCS MIDLA
 STA IACCL
MIDLA LDA IACCL
 BEQ RNUL
MIDLP LDAAX STRACC
 STAAY STRACC
 INY
 INX
 CPY IACCL
 BNE MIDLP
 STY CLEN
 BRA STRNX
STRD JSR AESPAC
 LDYIM &FF
 CMPIM "~"
 BEQ STRDT
 LDYIM 0
 DEC AECUR
STRDT PHY
 JSR FACTOR
 BEQ STRE
 TAY
 PLA
 STA PRINTF
 LDA VARL+3
 BNE STRDM
 STA WORK
 JSR FCONA
 BRA STRNX
STRDM JSR FCON
 BRA STRNX
STRE JMP LETM
STRND JSR INEXPR
 JSR PHACC
 JSR COMEAT
 JSR BRA
 BNE STRE
 JSR POPACC
 LDY CLEN
 BEQ STRNX
 LDA IACCL
 BEQ STRNY
 DEC IACCL
 BEQ STRNX
STRNL LDXIM 0
STRNLP LDAAX STRACC
 STAAY STRACC
 INX
 INY
 BEQ STRNOV
 CPX CLEN
 BCC STRNLP
 DEC IACCL
 BNE STRNL
 STY CLEN
STRNX LDAIM 0
 RTS
STRNY STA CLEN
 RTS
STRNOV JMP STROVR
 LNK CBAS0A
