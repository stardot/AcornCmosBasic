
;                              FILE 5 print dim misc > CBAS04
PRINTH JSR DAECHAN
PRINHL PHY
 JSR COMCHK
 BNE PRINHX
 JSR EXPR
 JSR STARGA
 PLY
 LDA TYPE
 JSR OSBPUT
 TAX
 BEQ PRINHS
 BMI PRINHF
 LDXIM 3
PRINHQ LDAAX IACCL
 JSR OSBPUT
 DEX
 BPL PRINHQ
 BRA PRINHL
PRINHF LDXIM 4
PRINHP LDAAX FWSA
 JSR OSBPUT
 DEX
 BPL PRINHP
 BRA PRINHL
PRINHS LDA CLEN
 JSR OSBPUT
 TAX
 BEQ PRINHL
PRINHO LDAAX STRACC-1
 JSR OSBPUT
 DEX
 BNE PRINHO
 BRA PRINHL
PRINHX PLA
 STY CURSOR
 JMP DONEXT
PRINT JSR CHKHSH
 BEQ PRINTH
 DEC CURSOR
 BRA STRTPR
PRCOMM LDA VARL
 BEQ STRTPR
 LDA TALLY
PRCOML BEQ STRTPR
 SBC VARL
 BCS PRCOML
 TAY
PRCOMO JSR LISTPT
 INY
 BNE PRCOMO
STRTPR CLC
 LDA VARL
 STA PRINTS
AMPER ROR PRINTF
ENDPRI JSR SPACES
 CMPIM ":"
 BEQ DEDPRC
 CMPIM &0D
 BEQ DEDPRC
 CMPIM TELSE
 BNE CONTPR
DEDPRC JSR NLINE
DEDPR JMP SUNK
PRFUNY CLR PRINTS
 CLR PRINTF
 JSR SPACES
 CMPIM ":"
 BEQ DEDPR
 CMPIM &0D
 BEQ DEDPR
 CMPIM TELSE
 BEQ DEDPR
CONTPR CMPIM "~"
 BEQ AMPER
 CMPIM ","
 BEQ PRCOMM
 CMPIM ";"
 BEQ PRFUNY
 JSR PRSPEC
 BCC ENDPRI
 LDA PRINTS
 PHA
 LDA PRINTF
 PHA
 DEC AECUR
 JSR EXPR
 PLA
 STA PRINTF
 PLA
 STA PRINTS
 LDA AECUR
 STA CURSOR
 TYA
 BEQ PSTR
 JSR FCON ;print a number
 LDA PRINTS
 SEC
 SBC CLEN
 BCC PSTR
 BEQ PSTR
 TAY
FPRNL JSR LISTPT
 DEY
 BNE FPRNL
PSTR LDA CLEN
 BEQ ENDPRI
 LDYIM 0
LOOP LDAAY STRACC
 JSR CHOUT+4
 INY
 CPY CLEN
 BNE LOOP
 BRA ENDPRI
TABCOM JMP COMERR
TAB2 LDA IACCL
 PHA
 JSR INTBRA
 LDAIM &1F
 JSR OSWRCH
 PLA
 JSR OSWRCH
 JSR WRIACC
 BRA PRTSTM
TAB JSR INEXPR
 JSR COMCHK
 BEQ TAB2
 CMPIM ")"
 BNE TABCOM
 LDA IACCL
 SBC TALLY ;carry set by cmp
 BEQ PRTSTM
 TAX
 BCS SPCLOP
 JSR NLINE
 BRA SPCT
SPC JSR INTFAC
SPCT LDX IACCL
 BEQ PRTSTM
SPCLOP JSR LISTPL
 BRA PRTSTM
PRCR JSR NLINE
PRTSTM CLC
 BRA ASCUR
ADEXPR DEC CURSOR
ASEXPR JSR AEEXPR
 JSR INTEGB
ASCUR LDY AECUR
 STY CURSOR
 RTS
PRSPEC LDX LINE
 STX AELINE
 LDX LINE+1
 STX AELINE+1
 LDX CURSOR
 STX AECUR
 CMPIM "'"
 BEQ PRCR
 CMPIM TTAB
 BEQ TAB
 CMPIM TSPC
 BEQ SPC
 SEC
PRTSTO RTS
NSTNG BRK
 = &09
 = &8D,""""
 BRK
PRTSTN JSR SPACES
 JSR PRSPEC
 BCC PRTSTO
 CMPIM """"
 BNE PRTSTO-1
PRSTRN INY
 LDAIY AELINE
 CMPIM &0D
 BEQ NSTNG
 CMPIM """"
 BNE PCH
 INY
 STY AECUR
 LDAIY AELINE
 CMPIM """"
 BNE PRTSTM
PCH JSR CHOUT+4
 BRA PRSTRN
CALL JSR AEEXPR
 JSR INTEG
 JSR PHACC
 CLR STRACC
 LDYIM 0
CALLLP PHY
 JSR COMCHK
 BNE CALLDN
 LDY AECUR
 JSR LVBLNK+1
 BEQ CALLFL
 PLY
 INY
 LDA IACCL
 STAAY STRACC
 INY
 LDA IACCM
 STAAY STRACC
 INY
 LDA IACCN
 STAAY STRACC
 INC STRACC
 BRA CALLLP
CALLDN PLY
 DEC AECUR
 JSR AEDONE
 JSR POPACC
 JSR USER
 CLD
 JMP NXT
CALLFL JMP FACERR
USER LDA VARL+12
 LSRA
 LDA VARL+4
 LDX VARL+&60
 LDY VARL+&64
 JMI IACCL
DELDED JMP STDED
DELETE JSR SPTSTN
 BCC DELDED
 JSR PHACC
 JSR CHKCOM
 BNE DELDED
 JSR SPTSTN
 BCC DELDED
 JSR DONE
 LDA IACCL
 STA WORK+2
 LDA IACCM
 STA WORK+3
 JSR POPACC
DODELS JSR REMOVE
 JSR TSTBRK
 JSR INCACC
 LDA WORK+2
 CMP IACCL
 LDA WORK+3
 SBC IACCM
 BCS DODELS
 JMP FSASET
GETTWO LDAIM 10
 JSR SINSTK
 JSR SPTSTN
 JSR PHACC
 LDAIM 10
 JSR SINSTK
 JSR CHKCOM
 BNE NO
 JSR SPTSTN
 LDA IACCM
 BNE GETYUK
 LDA IACCL
 BEQ GETYUK
 JMP DONE
NO JMP DONET
RENSET LDA TOP
 STA WORK+4
 LDA TOP+1
 STA WORK+5
RENSTR LDA TXTP
 STA WORK+1
 LDYIM 1
 STY WORK
 RTS
RENUM JSR GETTWO
 LDXIM WORK+2
 JSR POPX
 JSR ENDER
 JSR RENSET
NUMBA LDAI WORK
 BMI NUMBB
 STAI WORK+4
 LDAIY WORK ;Y=1
 STAIY WORK+4
 SEC
 TYA
 ADC WORK+4
 STA WORK+4
 BCC .+4
 INC WORK+5
 CMP HIMEM
 LDA WORK+5
 SBC HIMEM+1
 BCS NUMBFL
 JSR STEPON ;Y=1
 BRA NUMBA
NUMBFL BRK
 BRK
 = TRENUM," space"
GETYUK BRK
 BRK
 = "Silly"
 BRK
NUMBB JSR RENSTR
NUMBC LDAI WORK
 BMI NUMBD
 LDA WORK+3
 STAI WORK
 LDA WORK+2
 STAIY WORK ;Y=1
 CLC
 LDA WORK+2
 ADC IACCL
 STA WORK+2
 LDAIM 0
 ADC WORK+3
 ANDIM &7F
 STA WORK+3
 JSR STEPON ;Y=1
 BRA NUMBC
NUMBD LDA TXTP
 STA LINE+1
 CLR LINE
NUMBE LDYIM 1
 LDAIY LINE
 BMI NUMBX
 LDYIM 4
 CLR IACCN
NUMBF LDAIY LINE
 LDX IACCN
 BNE NUMBFS
 CMPIM TCONST
 BEQ NUMBG
 CMPIM TREM
 BEQ NUMBFX
NUMBFS INY
 CMPIM """"
 BNE NUMBFT
 EOR IACCN
 STA IACCN
NUMBFT CMPIM &0D
 BNE NUMBF
NUMBFX LDYIM 3
 LDAIY LINE
 JSR ADDLIN
 BRA NUMBE
NUMBG JSR SPGETN
 JSR RENSET ;Y=1
NUMBH LDAI WORK
 BMI NUMBJ
 LDAI WORK+4
 CMP IACCM
 BNE NUMBI
 LDAIY WORK+4 ;Y=1
 CMP IACCL
 BNE NUMBI
 LDAIY WORK ;Y=1
 STA WORK+6
 LDAI WORK
 TAX
 LDY CURSOR
 DEY
 LDA LINE
 STA WORK+2
 LDA LINE+1
 STA WORK+3
 JSR CONSTI
NUMBFA LDY CURSOR
 BRA NUMBF
NUMBI CLC
 JSR STEPON
 LDA WORK+4
 ADCIM 2 ;carry clear from stepon
 STA WORK+4
 BCC NUMBH
 INC WORK+5
 BRA NUMBH
NUMBX BRA ENDAUT
NUMBJ JSR VSTRNG
 = "Failed at "
 LDAIY LINE ;Y=1
 STA IACCM
 INY
 LDAIY LINE
 STA IACCL
 JSR POSITE
 JSR NLINE
 BRA NUMBFA
STEPON INY
 LDAIY WORK
 LDYIM 1
 ADC WORK
 STA WORK
 BCC STEPX
 INC WORK+1
 CLC
STEPX RTS
AUTO JSR GETTWO
 LDA IACCL
 PHA
 JSR POPACC
AUTOLP JSR PHACC
 JSR NPRN
 JSR BUFF
 JSR POPACC
 JSR MATCH
 LDYIM 0
 JSR INSRT
 JSR SETFSA
 PLA
 PHA
 CLC
 ADC IACCL
 STA IACCL
 BCC AUTOLP
 INC IACCM
 BPL AUTOLP
ENDAUT JMP FSASET
DIMSPR JMP DIMRAM
DIMSP DEC CURSOR
 JSR CRAELV
 BEQ NOTGO
 BCS NOTGO
 JSR PHADDR
 JSR INEXPR
 JSR INCACC
 LDA IACCH
 ORA IACCN
 BNE NOTGO
 CLC
 LDA IACCL
 ADC FSA
 TAY
 LDA IACCM
 ADC FSA+1
 TAX
 CPY AESTKP
 SBC AESTKP+1
 BCS DIMSPR
 LDA FSA
 STA IACCL
 LDA FSA+1
 STA IACCM
 STY FSA
 STX FSA+1
;CLR IACCN
;CLR IACCH N and H already zero
 LDAIM &40
 STA TYPE
 JSR STORE
 JSR ASCUR
DIMNXT JSR CHKCOM
 BEQ DIM
 JMP SUNK
SMUL LDXIM WORK+8
 JSR POPX
WMUL LDXIM 0
 LDYIM 0
SMULA LSR WORK+9
 ROR WORK+8
 BCC SMULB
 CLC
 TYA
 ADC IACCL
 TAY
 TXA
 ADC IACCM
 TAX
 BCS NOTGO
SMULB ASL IACCL
 ROL IACCM
 BCS NOTGO
 LDA WORK+8
 ORA WORK+9
 BNE SMULA
 ASSERT :MSB: SMULA = :MSB: .
 STY IACCL
 STX IACCM
 RTS
NOTGO BRK
 = &0A
 = "Bad ",TDIM
 BRK
DIM JSR SPACES
 TYA
 CLC
 ADC LINE
 LDX LINE+1
 BCC DIMNNO
 INX
 CLC
DIMNNO SBCIM 0
 STA WORK
 TXA
 SBCIM 0
 STA WORK+1
 LDXIM 5
 STX WORK+8
 LDX CURSOR
 JSR WORD
 CPYIM 1
 BEQ NOTGO
 CMPIM "("
 BEQ DIMVAR
 CMPIM "$"
 BEQ DIMINT
 CMPIM "%"
 BNE DIMSPA
DIMINT DEC WORK+8
 INY
 INX
 LDAIY WORK
 CMPIM "("
 BEQ DIMVAR
DIMSPA JMP DIMSP
DIMVAR INY
 STX CURSOR
 JSR LOOKUP
DIMGON BNE NOTGO ;NE chain branch to NOTGO
 JSR CREATE
 LDXIM 1
 JSR CREAX
 LDA WORK+8
 PHA
 LDAIM 1
 PHA
 JSR SINSTK
RDLOOP JSR PHACC
 JSR ASEXPR
 LDA IACCM
 ANDIM &C0
 ORA IACCN
 ORA IACCH
 BNE NOTGO
 JSR INCACC
 PLY
 LDA IACCL
 STAIY FSA
 INY
 LDA IACCM
 STAIY FSA
 INY
 PHY
 JSR SMUL
 JSR CHKCOM
 BEQ RDLOOP
 CMPIM ")"
 BNE DIMGON ;warning NE status used again
 PLX
 PLA
 PHX
 STA WORK+8
 CLR WORK+9
 JSR WMUL
 PLA
 PHA
 ADC IACCL
 STA IACCL
 BCC NOHINC
 INC IACCM
NOHINC LDA FSA+1
 STA WORK+1
 LDA FSA
 STA WORK
 CLC
 ADC IACCL
 TAY
 LDA IACCM
 ADC FSA+1
 BCS DIMRAM
 TAX
 CPY AESTKP
 SBC AESTKP+1
 BCS DIMRAM
 STY FSA
 STX FSA+1
 PLA
 STAI WORK
 ADC WORK
 TAY
 LDAIM 0
 CLR WORK
 BCC ZELOOP
 INC WORK+1
ZELOOP STAIY WORK
 INY
 BNE ZELPA
 INC WORK+1
ZELPA CPY FSA
 BNE ZELOOP
 CPX WORK+1
 BNE ZELOOP
 JMP DIMNXT
DIMRAM BRK
 = &0B
 = TDIM," space"
 BRK
LHIMM JSR INEQEX
 LDA IACCL
 STA HIMEM
 STA AESTKP
 LDA IACCM
 STA HIMEM+1
 STA AESTKP+1
 BRA LPAGEX
LLOMM JSR INEQEX
 LDA IACCL
 STA LOMEM
 STA FSA
 LDA IACCM
 STA LOMEM+1
 STA FSA+1
 JSR SETVAL
 BRA LPAGEX
LPAGE JSR INEQEX
 LDA IACCM
 STA TXTP
LPAGEX JMP NXT
CLEAR JSR DONE
 JSR SETFSA
 BRA LPAGEX
TRACE JSR SPTSTN
 BCS TRACNM
 CMPIM TON
 BEQ TRACON
 CMPIM TOFF
 BEQ TRACOF
 JSR ASEXPR
TRACNM JSR DONE
 LDA IACCL
 STA TRNUM
 LDA IACCM
TRACNO STA TRNUM+1
 LDAIM &FF
TRACNN STA TRFLAG
 BRA LPAGEX
TRACON INC CURSOR
 JSR DONE
 LDAIM &FF
 BNE TRACNO
TRACOF INC CURSOR
 JSR DONE
 LDAIM 0
 BRA TRACNN
LTIME INY
 LDAIY LINE
 CMPIM "$"
 BEQ LTIMED
 JSR INEQEX
 CLR IACCL+4
 LDXIM IACCL
 LDYIM 0
 LDAIM 2
LTIMEX JMP OSWNXT
LTIMED INC CURSOR
 JSR AEEQEX
 LDA TYPE
 BNE INTEGE
 LDAIM &F ;set time$
 LDY CLEN
 STY STRACC-1
 LDXIM STRACC-1
 LDYIM /(STRACC-1)
 BRA LTIMEX
STINBR JSR PHSTR
INTBRA JSR BRA
 BRA INTEGB
INCMEX JSR COMEAT
INEXPR JSR EXPR
 BRA INTEGB
INTFAC JSR FACTOR
 BRA INTEGB
INEQEX JSR AEEQEX
INTEG LDA TYPE
INTEGY TAY
INTEGB BEQ INTEGE
 BPL INTEGX
IFIX JSR SFIX
 LDA FACCMA
 STA IACCH
 LDA FACCMB
 STA IACCN
 LDA FACCMC
 STA IACCM
 LDA FACCMD
 STA IACCL
INTEGX RTS
INTEGE JMP LETM
FLTFAC JSR FACTOR
FLOATI BEQ INTEGE
 BMI INTEGX
 JMP IFLT
PROC LDA LINE
 STA AELINE
 LDA LINE+1
 STA AELINE+1
 LDA CURSOR
 STA AECUR
 LDAIM TPROC
 JSR FNBODY
 JSR AEDONE
 JMP NXT
LOCSTR LDYIM 3
 LDAIM 0
 STAIY IACCL
 BEQ LOCVAR
LOCAL TSX
 CPXIM &FC
 BCS NLOCAL
 JSR CRAELV
 BEQ LOCEND
 JSR RETINF
 LDY IACCN
 BMI LOCSTR
 JSR PHADDR
 JSR FALSE
 STA TYPE
 JSR STORE
LOCVAR TSX
 INCAX &0106
 LDY AECUR
 STY CURSOR
 JSR CHKCOM
 BEQ LOCAL
 JMP SUNK
LOCEND JMP DONEXT
NLOCAL BRK
 = &0C
 = "Not ",TLOCAL
MODESX BRK
 = &19
 = "Bad ",TMODE
 BRK
GRAPH JSR ASEXPR
 LDA IACCL
 PHA
 JSR INCMEX
 JSR AEDONE
 LDAIM &12
 JSR OSWRCH
 PLA
 BRA SENTWO
TEXTS JSR ASEXPR
 JSR DONE
 LDAIM &11
 BRA SENTWO
MODES JSR ASEXPR
 JSR DONE
 JSR GETMAC
 INX
 BNE MODEGO
 INY
 BNE MODEGO
 LDA AESTKP
 CMP HIMEM
 BNE MODESX
 LDA AESTKP+1
 CMP HIMEM+1
 BNE MODESX
 LDX IACCL
 LDAIM &85
 JSR OSBYTE
 CPX FSA
 TYA
 SBC FSA+1
 BCC MODESX
 CPX TOP
 TYA
 SBC TOP+1
 BCC MODESX
 STX HIMEM
 STX AESTKP
 STY HIMEM+1
 STY AESTKP+1
MODEGO CLR TALLY
 LDAIM &16
SENTWO JSR OSWRCH
 LDA IACCL
 BRA DOCL
MOVE LDAIM 4
 BRA DRAWER
DRAW LDAIM 5
DRAWER PHA
 JSR AEEXPR
 JSR INTEG
 BRA PLOTER
PLOT JSR ASEXPR
 LDA IACCL
 PHA
 JSR INCMEX
PLOTER JSR PHACC
 JSR INCMEX
 JSR AEDONE
 LDAIM &19
 JSR OSWRCH
 PLA
 JSR OSWRCH
 JSR POPWRK
 LDA WORK
 JSR OSWRCH
 LDA WORK+1
 JSR OSWRCH
 JSR WRIACC
 LDA IACCM
 BRA DOCL
CLG JSR DONE
 LDAIM &10
 BRA DOCL
CLS JSR DONE
 CLR TALLY
 LDAIM &0C
DOCL JSR OSWRCH
CLSEXT JMP NXT
REPORT JSR DONE
 JSR NLINE
 LDYIM 1
REPLOP LDAIY REPPTR
 BEQ CLSEXT
 JSR TOKOUT
 INY
 BNE REPLOP
 BRA CLSEXT
VDUP LDA IACCM
 JSR OSWRCH
VDU JSR SPACES
VDUL CMPIM ":"
 BEQ VDUX
 CMPIM &0D
 BEQ VDUX
 CMPIM TELSE
 BEQ VDUX
 DEC CURSOR
 JSR ASEXPR
 JSR WRIACC
 JSR CHKCOM
 BEQ VDU
 CMPIM ";"
 BEQ VDUP
 CMPIM "|"
 BNE VDUL
 LDAIM 0
 LDYIM 9
VDUFLL JSR OSWRCH
 DEY
 BNE VDUFLL
 BRA VDU
VDUX JMP SUNK
WRIACC LDA IACCL
 JMI &20E
 LNK CBAS05
